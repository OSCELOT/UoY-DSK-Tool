apply plugin: "java"
apply plugin: "war"
apply plugin: "maven"

// define the version for the project when publishing to maven
group "york.b2.dsk"
version getB2Version()

project.ext {
    learnVersion = "3100.0.0-rel.107"
    springVersion = "5.0.6.RELEASE"
    hibernateVersion = "5.3.2.Final"
    tilesVersion="3.0.8"
    slf4jVersion="1.6.6"
}

String getB2Version() {
    File mfFile = new File(file("src/main/webapp"), 'WEB-INF/bb-manifest.xml')
    def manifest = new XmlSlurper().parse(mfFile)
    return manifest.plugin.version['@value'].toString()+"-RELEASE"

}

repositories {
    mavenCentral()
    maven { url "https://maven.blackboard.com/content/repositories/releases/" }
}

configurations {
    //https://www.concretepage.com/build-tools/gradle/gradle-exclude-transitive-dependency-example
    //compile.exclude module: 'dom4j'
    // Removing xml-apis to make it work with OpenJDK11 in SaaS
    compile.exclude module: 'xml-apis'
    buildUtils
}

// define the project's dependencies
dependencies {
    providedCompile "javax.servlet:servlet-api:2.5"
    providedCompile "javax.servlet:jstl:1.2"
    providedCompile "javax.servlet.jsp:jsp-api:2.0"

    // providedCompile dependencies are libraries needed to build, but should not be included in the B2 WAR.
    providedCompile("blackboard.platform:bb-platform:${project.learnVersion}") { transitive = false }
    providedCompile("blackboard.platform:bb-cms-admin:${project.learnVersion}") { transitive = false }
    providedCompile("blackboard.platform:bb-taglibs:${project.learnVersion}") { transitive = false }
    providedCompile("blackboard.platform:bb-bbxythos:${project.learnVersion}") { transitive = false }
    compile("blackboard.platform:bb-spring-webapi:${project.learnVersion}") { transitive = false }

    compile( "org.hibernate:hibernate-core:$project.hibernateVersion" ) { transitive = true }
    compile "org.hibernate:hibernate-entitymanager:$project.hibernateVersion"

    compile "org.slf4j:slf4j-api:${project.slf4jVersion}"
    compile "org.slf4j:jcl-over-slf4j:${project.slf4jVersion}"
    compile "org.slf4j:slf4j-log4j12:${project.slf4jVersion}"

    compile "org.apache.tiles:tiles-api:${project.tilesVersion}"
    compile "org.apache.tiles:tiles-core:${project.tilesVersion}"
    compile "org.apache.tiles:tiles-jsp:${project.tilesVersion}"

    compile "commons-io:commons-io:2.4"
    compile "commons-codec:commons-codec:1.6"
    compile "org.apache.commons:commons-lang3:3.1"

    compile "org.springframework:spring-webmvc:${project.springVersion}"

    // For autowired
    compile("com.fasterxml.jackson.core:jackson-databind:2.9.5")
    // To fix: Cannot convert value of type 'com.fasterxml.jackson.dataformat.cbor.CBORFactory' to required type
    // 'com.fasterxml.jackson.core.JsonFactory'
    compile("com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.9.5")

    // Needed for SimpleDAO<T>
    compile "com.google.guava:guava:23.0"

    compile group: 'org.ostermiller', name: 'utils', version: '1.07.00'

    runtime "org.javassist:javassist:3.17.1-GA", "dom4j:dom4j:1.6.1"

    buildUtils "org.oscelot:b2deploy-task:0.1.0"

    // v2.5.3 fix the javax.annotation.Resource issue with OpenJDK 11
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
}

// Add a task to deploy a B2 using starting block
task deployB2(dependsOn: "war") << {
    ant.taskdef(name: "b2deploy", classname: "org.oscelot.ant.B2DeployTask", classpath: project.configurations.buildUtils.asPath)
    ant.b2deploy(localfilepath: project.war.archivePath, host: "http://localhost:9876", courseorgavailable: 'true', clean: 'true')
}
